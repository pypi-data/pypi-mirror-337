######################################################
# Flint & Slutzky (2012)
######################################################

DATASET = "flint_slutzky_accurate_2012"

RAW_DIR = config["RAW_DIR"]
PROCESSED_DIR = config["PROCESSED_DIR"]


checkpoint download_data:
    output:
        f"{RAW_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        mkdir -p {RAW_DIR}/{DATASET}
        wget -O "{RAW_DIR}/{DATASET}/Flint_2012_e1.mat" "https://portal.nersc.gov/project/crcns/download/dream/data_sets/Flint_2012?fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e1.mat&username=&password=&guest=1&fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e1.mat&agree_terms=on&submit=Login+Anonymously"
        wget -O "{RAW_DIR}/{DATASET}/Flint_2012_e2.mat" "https://portal.nersc.gov/project/crcns/download/dream/data_sets/Flint_2012?fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e2.mat&username=&password=&guest=1&fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e2.mat&agree_terms=on&submit=Login+Anonymously"
        wget -O "{RAW_DIR}/{DATASET}/Flint_2012_e3.mat" "https://portal.nersc.gov/project/crcns/download/dream/data_sets/Flint_2012?fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e3.mat&username=&password=&guest=1&fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e3.mat&agree_terms=on&submit=Login+Anonymously"
        wget -O "{RAW_DIR}/{DATASET}/Flint_2012_e4.mat" "https://portal.nersc.gov/project/crcns/download/dream/data_sets/Flint_2012?fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e4.mat&username=&password=&guest=1&fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e4.mat&agree_terms=on&submit=Login+Anonymously"
        wget -O "{RAW_DIR}/{DATASET}/Flint_2012_e5.mat" "https://portal.nersc.gov/project/crcns/download/dream/data_sets/Flint_2012?fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e5.mat&username=&password=&guest=1&fn=dream%2Fdata_sets%2FFlint_2012%2FFlint_2012_e5.mat&agree_terms=on&submit=Login+Anonymously"
        find {RAW_DIR}/{DATASET}/ -type f -name "*.mat" | sed "s|^{RAW_DIR}/{DATASET}/||" | sed "s|^/*||" > {{output}}
        """

rule prepare_data:
    input:
        mat_file = f"{RAW_DIR}/{DATASET}/{{file}}"
    output:
        temp(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt")
    log:
        f".snakemake/logs/{DATASET}/prepare_data.{{file}}.log"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/{DATASET}/tmp
        python -m brainsets_pipelines.{DATASET}.prepare_data --input_file {{input.mat_file}} --output_dir {PROCESSED_DIR}/{DATASET} 2>&1 | tee {{log}}
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}
        """

def aggregate_input(wildcards):
    with checkpoints.download_data.get(**wildcards).output[0].open() as manifest:
        files = [line.strip() for line in manifest]
    return expand(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt", file=files)


rule merge_manifests:
    input:
        aggregate_input
    output:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}        
        """

rule all:
    input:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
